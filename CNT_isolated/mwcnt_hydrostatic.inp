! ANSYS Mechanical APL v16.1
!
! Hydrostatic test in a multi-wall carbon nanotube (MWCNT)
!
FINISH
/CLEAR

/TITLE,Multi-wall Carbon Nanotube simulation
/STITLE,1,Hydrostatic test

/FILNAME,MWCNT_hydro,0 ! Changes the jobname of the analysis

*del,all ! Delete all previous defined parameters
!-------------------------------------------------------------------------------------
! INTERACTIVE MENU

*ask,length_aprox,enter length of the CNT [nm],10
*ask,nlayer,enter number of concentric layer,1

*dim,nindex,array,nlayer
*dim,mindex,array,nlayer
*dim,ntest,array,nlayer+1
*dim,mtest,array,nlayer+1
ntest(1)=0
mtest(1)=0
ind=0

*DO,i,1,nlayer
    ntest(i+1)=ntest(i)+10
    mtest(i+1)=mtest(i)+10
    *ask,nindex(i),Enter n in (n,m) (zigzag or armachair only) in layer %i%,%ntest(i+1)%
    *ask,mindex(i),Enter m in (n,m) (zigzag or armachair only) in layer %i%,%mtest(i+1)%
*ENDDO

/INPUT,mwcnt_mesh,inp,

!-------------------------------------------------------------------------------------
! BOUNDARY CONDITIONS

! Selecting nodes at Z=0
tol=1e-4
Z0=0.0
nsel,s,loc,z,Z0-tol,Z0+tol

*GET,nselnodes,NODE,0,COUNT,    ! Number of selecte nodes
*DIM,num_0,,nselnodes,1         ! Vector of selected node numbers
*GET,minnode,NODE,0,NUM,MIN     ! Minimun node number in the selection
*VFILL,num_0(1,1),DATA,minnode

*DO,i,2,nselnodes
    *GET,nextnode,NODE,minnode,NXTH,    ! minnode: previous node number for each nextnode
    minnode=nextnode                    ! nextnode: each component of the above vector num
    *VFILL,num_0(i,1),DATA,minnode
*ENDDO

allsel,all

! Selecting nodes at Z=L
*GET,nzmax,node,0,mxloc,z

! Selecting nodes at Z=L/2
nsel,s,loc,z,nzmax-tol,nzmax+tol

*DIM,num_l,,nselnodes,1              ! Vector of selected node numbers
*get,minnode,NODE,0,NUM,MIN         ! Minimun node number in the selection
*VFILL,num_l(1,1),DATA,minnode

*DO,i,2,nselnodes
    *GET,nextnode,NODE,minnode,NXTH,    ! minnode: previous node number for each nextnode
    minnode=nextnode                    ! nextnode: each component of the above vector num
    *VFILL,num_l(i,1),DATA,minnode
*ENDDO

! Selecting all nodes in the outer surface
CSYS,1
nrotat,all
allsel,all

nsel,s,loc,X,radius-tol,radius+tol
CM,node_out,NODE

*GET,numnodes,NODE,0,COUNT
*DIM,num_all,,numnodes,1              ! Vector of selected node numbers
*get,minnode,NODE,0,NUM,MIN         ! Minimun node number in the selection
*VFILL,num_all(1,1),DATA,minnode

*DO,i,2,numnodes
    *GET,nextnode,NODE,minnode,NXTH,    ! minnode: previous node number for each nextnode
    minnode=nextnode                    ! nextnode: each component of the above vector num
    *VFILL,num_all(i,1),DATA,minnode
*ENDDO

allsel,all

!-------------------------------------------------------------------------------------
! BOUNDARY CONDITIONS

! Fixed ends
nrotat,all
allsel,all

D,num_0(1,1),UX,0.0
D,num_0(1,1),UY,0.0
*DO,i,1,nselnodes
    D,num_0(i,1),UZ,0.0
    D,num_l(i,1),UZ,0.0
*ENDDO

! Radial displacement
tens_disp = 0.01
*DO,i,1,numnodes
    D,num_all(i,1),UX,tens_disp
*ENDDO
! CSYS,0
! nrotat,all

/PBC,U,,1 ! to display boundary conditions (displacements)

!-------------------------------------------------------------------------------------
! SOLUTION
/SOLU

ANTYPE,STATIC       ! Static analysis
! NLGEOM,ON           ! Includes large-deflection (large rotation) effects or large strain effects, according to the element type.
! SOLCONTROL,ON       ! Advanced nonlinear options

! AUTOTS,ON           ! Automatic strategy
! TIME,0.5            ! Time at the end of the load step
! DELTIM,5.0e-2,1.0e-6,1.0e-1,ON ! ON: Use final time step from previous load step as the starting time step
! CNVTOL,F,,1e-3,0,1e-2  !try this value

!PSTRES,ON           ! Calculate prestress effects
!STABILIZE,constant,energy,1e-3,no
!CNVTOL,F,,1e-3,0,1e-2  !try this value
!DCUM,RPL
!EQSLV,SPARSE, ! Sparse solver

OUTRES,all,all

allsel,all

SOLVE

*GET,nSsteps,ACTIVE,0,SOLU,NCMSS ! Number of sub-steps

FINISH

!-------------------------------------------------------------------------------------
! POST-PROCESSING
/POST1

! Transversal area
AA_CNT = 2*PI*radius*nzmax

CMSEL,s,node_out,NODE

! Results calculation
*DIM,drad_l,,nSsteps,1
*DIM,FH_VECTOR,,nSsteps,1
*DIM,engstrain_22,,nSsteps,1
*DIM,rho_12,,nSsteps,1
*DIM,engstress,,nSsteps,1
*DIM,bulk_mod,,nSsteps,1
*DO,p,1,nSsteps
    SET,1,p
    drad_l(p)    = SQRT((UX(num_l(1,1)))**2+(UY(num_l(1,1)))**2)   ! Radial displacement
    
    FH_VECTOR(p) = 0.0
    *DO,i,1,numnodes
        *GET,FX_react,NODE,num_all(i,1),RF,FX
        FH_VECTOR(p) = FH_VECTOR(p)+FX_react
    *ENDDO
    
    engstrain_22(p) = drad_l(p)/radius                  ! Engineering strain (circumferential direction)
    engstress(p)    = FH_VECTOR(p)/AA_CNT               ! Engineering stress
    bulk_mod(p)     = engstress(p)/engstrain_22(p)      ! Bulk modulus
*ENDDO
 
*CFOPEN,results_hydro.txt,,
*VWRITE,'Disp. ','[nm]: ',drad_l(1),' Force',' [N]: ',FH_VECTOR(1),' Strain: ','  ',engstrain_22(1),' Stress ','[TPa]: ',engstress(1),' Bulk ',' [TPa]: ',bulk_mod(1)
(A7,A7,G16.8,A6,A5,G16.8,A8,A7,G16.8,A8,A8,G16.8,A7,A8,G16.8)
*CFCLOS

! View deformed shape
/VIEW,1,1,1,1   
/ANG,1   
/REP,FAST   
ESEL,S,TYPE,,1  
EPLOT   
FINISH  
/POST1
SET,LAST
PLDISP,1

FINISH
